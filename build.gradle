buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
        classpath 'com.fsryan.gradle.autosemver:autosemver-git:0.2.0-beta'
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.2.10'
    id 'java-gradle-plugin'
}

apply plugin: 'com.jfrog.bintray'
apply plugin: 'kotlin'
apply plugin: 'autosemver-git'

repositories {
    jcenter()
}

dependencies {
    compileOnly gradleApi()

    implementation 'org.jetbrains.kotlin:kotlin-stdlib:1.2.10'

    testCompileOnly gradleTestKit()
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.assertj:assertj-core:3.6.2'
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

ext {
    libDesc = 'Gradle plugin companion to the forsuredb project'
    licenses = ['Apache-2.0']
    vcsUrl = 'https://github.com/ryansgot/forsuredbplugin.git'
    artifactId = 'forsuredb-plugin'
    buildTime = Long.toString(new Date().getTime())
}

group 'com.fsryan.gradle.forsuredb'

autosemver {
    branchConfigs {
        integration {
            skipCiCommitMessageSuffix = '[skip ci]'
            pullRemote = 'origin'
            pushRemote = 'origin'
            versionIncrement = "patch"
            preRelease = 'test'
            metaData = project.buildTime
        }
        alpha { // <-- for new features
            skipCiCommitMessageSuffix = '[skip ci]'
            pullRemote = 'origin'
            pushRemote = 'origin'
            versionIncrement = "minor"
            preRelease = 'alpha'
        }
        beta { // <-- for bug fixes
            skipCiCommitMessageSuffix = '[skip ci]'
            pullRemote = 'origin'
            pushRemote = 'origin'
            preRelease = 'beta'
        }
        master {    // <-- stable version
            skipCiCommitMessageSuffix = '[skip ci]'
            pullRemote = 'origin'
            pushRemote = 'origin'
        }
    }
}

apply from: 'publish.gradle'